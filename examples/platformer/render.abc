constexpr sprites TILES = sprites{ 16x16 "assets/tiles.png" };
constexpr sprites PLAYER = sprites{ 16x16 "assets/player.png" };

constexpr font FONT = FONT_BR6;

void render()
{
    // setup
    int icamx = int($round(camx));
    int icamy = int($round(camy));

    // clamp camera to edge of map
    {
        int xmax = map_width() * 16 - 129;
        int ymax = map_height() * 16 - 65;

        if(icamx < 0) icamx = 0;
        if(icamy < 0) icamy = 0;
        if(icamx > xmax) icamx = xmax;
        if(icamy > ymax) icamy = ymax;
    }

    // render map tiles
    {
        i8 cx = icamx / 16;
        i8 cy = icamy / 16;
        i8 xoff = -(icamx & 15);
        i8 yoff = -(icamy & 15);
        u8[] prog& m = maps[current_map].map;
        u8 w = map_width();

        for(u8 i = 0; i < 5; ++i)
        {
            u16 index = (i + cy) * w + cx;
            for(u8 j = 0; j < 9; ++j)
            {
                u8 t = m[index + j] - 1;
                if(t < 240)
                    $draw_sprite(j * 16 + xoff, i * 16 + yoff, TILES, t);
            }
        }
    }

    // render objects
    {
        for(u8 i = 0; i < MAX_OBJECTS; ++i)
        {
            map_object_t obj = objects[i];
            if(obj.type == OBJ_NONE)
                continue;
            u8 t = 241;

            $draw_sprite_selfmask(
                obj.x - icamx, obj.y - icamy, TILES,
                t + ((frame_count + i * 173) & 0x30));
        }
    }

    // render player
    {
        u8 index = 0;
        if(player_facing_right)
            index += 7;
        if(player_on_ground)
        {
            if(player_moving)
                index += ((frame_count >> 3) & 3);
        }
        else
        {
            index += 4;
        }

        float x = px - camx - PXOFF;
        float y = py - camy - PYOFF;
        $draw_sprite_selfmask($round(x), $round(y), PLAYER, index);
    }
}
