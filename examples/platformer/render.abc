constexpr sprites TILES = sprites{ 16x16 "assets/tiles.png" };
constexpr sprites PLAYER = sprites{ 16x16 "assets/player.png" };
constexpr sprites IMG_KEY = sprites{ "assets/key.png" };

constexpr font FONT = FONT_BR6;

void render()
{
    // setup
    int icamx = int($round(camx));
    int icamy = int($round(camy));

    // render map tiles
    {
        i8 cx = icamx / 16;
        i8 cy = icamy / 16;
        i8 xoff = -(icamx & 15);
        i8 yoff = -(icamy & 15);
        u8[] prog& m = maps[current_map].map;
        u8 w = map_width();

        // background pattern
        $draw_sprite(xoff, yoff, sprites{ "assets/background.png" }, 0);

        for(u8 i = 0; i < 5; ++i)
        {
            u16 index = (i + cy) * w + cx;
            for(u8 j = 0; j < 9; ++j)
            {
                u8 t = m[index + j] - 1;
                if(t >= 240)
                    continue;
                if(t >= 176 && ((frame_count + 0x05) & 0x08))
                    t += 32;
                $draw_sprite(j * 16 + xoff, i * 16 + yoff, TILES, t);
            }
        }
    }

    // render objects
    {
        for(u8 i = 0; i < MAX_OBJECTS; ++i)
        {
            map_object_t obj = objects[i];
            u8 type = obj.type;
            if(type == OBJ_NONE)
                continue;
            u8 t = 240 + type;

            $draw_sprite_selfmask(
                obj.x - icamx, obj.y - icamy, TILES,
                t + ((frame_count + i * 173) & 0x30));
        }
    }

    // render player
    if(invincibility_frames == 0 || (frame_count & 0x04) == 0)
    {
        u8 index = 0;
        if(player_facing_right)
            index += 7;
        if(player_on_ground)
        {
            if(player_moving)
                index += ((frame_count >> 3) & 3);
        }
        else
        {
            index += 4;
        }

        float x = px - camx - PXOFF;
        float y = py - camy - PYOFF;
        $draw_sprite($round(x), $round(y), PLAYER, index);
    }

    // render HUD elements
    for(u8 i = 0; i < keys; ++i)
        $draw_sprite(119 - i * 9, 0, IMG_KEY, 0);
    $draw_filled_rect(0, 0, 24, 6, BLACK);
    $draw_textf(0, 0, FONT_BR6, "%u", score);
}
