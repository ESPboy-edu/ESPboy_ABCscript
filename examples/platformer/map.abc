import vars;
import mapdata;

struct map_info_t
{
    char[] prog& name;
    u8[] prog& map;
    u8 w, h;
};

map_info_t[NUM_MAPS] prog maps = {
    { "Sleepy Meadow", map0, 32, 8 },
    { "Daring Forest", map1, 32, 16 },
    { "Grand Old Tree", map2, 16, 32 },
    { "Luke's Challenge", map_luke, 32, 8 },
};

u16 total_objects;

void load_map()
{
    player_facing_right = true;
    keys = 0;
    keys_required = 0;
    invincibility_frames = INVINCIBILITY_LEVEL_START;
    jump_frames_remaining = 0;
    vx = 0;
    vy = 0;
    level_start_frames = LEVEL_START_FRAMES;
    death_frames = 0;
    state = STATE_LEVEL;
    score = 0;
    hearts = MAX_HEARTS;
    level_frames_remaining = TIME_LIMIT_FRAMES;

    u8[] prog& map = maps[current_map].map;
    u8 w = maps[current_map].w;
    u8 h = maps[current_map].h;
    map_width = w;
    map_height = h;

    // reset map objects
    for(u8 i = 0; i < MAX_OBJECTS; ++i)
        objects[i] = { 0, 0, OBJ_NONE };

    total_objects = 0;

    // look through all map tiles for objects
    for(u8 y = 0; y < h; ++y)
    {
        for(u8 x = 0; x < w; ++x)
        {
            u8 t = map[y * w + x] - 1;
            process_map_tile(x, y, t);
        }
    }

    if(total_objects > MAX_OBJECTS)
    {
        $debug_printf("Objects: %u\n", total_objects);
        $assert(false);
    }

    $tones_play(SOUND_LEVEL_START);
}

void process_map_tile(u8 x, u8 y, u8 t)
{
    if(t < 240) return;

    if(t == TILE_PLAYER)
    {
        // set initial player/camera position
        px = x * 16 + PXOFF;
        py = y * 16 + PYOFF;
        camx = px - CAMERA_OFFSET_X + CAMERA_OFFSET_X_DELTA;
        camy = py - CAMERA_OFFSET_Y;
        float xmax = map_width * 16 - 129;
        float ymax = map_height * 16 - 65;
        if(camx < 0) camx = 0;
        if(camy < 0) camy = 0;
        if(camx > xmax) camx = xmax;
        if(camy > ymax) camy = ymax;
        dcamx = 0;
    }
    else if(t == TILE_COIN)
    {
        add_object({ OBJ_COIN, x * 16, y * 16, 3, 3, 10, 10 });
    }
    else if(t == TILE_SUPERCOIN)
    {
        add_object({ OBJ_SUPERCOIN, x * 16, y * 16, 3, 3, 10, 10 });
    }
    else if(t == TILE_KEY)
    {
        add_object({ OBJ_KEY, x * 16, y * 16, 2, 4, 12, 8 });
    }
    else if(t == TILE_HEART)
    {
        add_object({ OBJ_HEART, x * 16, y * 16, 2, 3, 12, 10 });
    }
    else if(t >= TILE_MOB && t <= TILE_MOB_HELM)
    {
        u8 xo = 3;
        u8 yo = 5;
        if(t == TILE_MOB_HELM)
        {
            xo = 2;
            yo = 3;
        }
        add_object({ t - 240, x * 16, y * 16, xo, yo, 16 - xo * 2, 16 - yo, true });
    }
    else if(t >= TILE_DOOR0 && t <= TILE_DOOR3)
    {
        keys_required = t - TILE_DOOR0;
    }
}

void add_object(map_object_t obj)
{
    ++total_objects;
    for(u8 i = 0; i < MAX_OBJECTS; ++i)
    {
        if(objects[i].type == OBJ_NONE)
        {
            objects[i] = obj;
            return;
        }
    }
    
    // too many items in the map!
    //$assert(false);
}
