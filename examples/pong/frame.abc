void bounce_ball_off_paddle()
{
    vx = -vx;
    vx = vx * 17 / 16;
    vy = vy * 17 / 16;
    $tones_play(SFX_BOUNCE_PADDLE);
}

void bounce_ball_off_wall()
{
    vy = -vy;
    $tones_play(SFX_BOUNCE_WALL);
}

void update()
{
    $poll_buttons();

    if($pressed(UP_BUTTON))
        py0 = py0 - PADDLE_SPEED;
    if($pressed(DOWN_BUTTON))
        py0 = py0 + PADDLE_SPEED;
        
    if(!ai_serving && vx > 0 && bx > 40 * FRAC)
    {
        int dy = by - py1 - (PADDLE_HEIGHT - BALL_SIZE) * FRAC / 2;
        if(dy > vy)
            py1 = py1 + PADDLE_SPEED;
        if(dy < vy)
            py1 = py1 - PADDLE_SPEED;
    }
    
    if(py0 < TOP_Y * FRAC)
        py0 = TOP_Y * FRAC;
    if(py0 > (BOT_Y - PADDLE_HEIGHT) * FRAC)
        py0 = (BOT_Y - PADDLE_HEIGHT) * FRAC;
    if(py1 < TOP_Y * FRAC)
        py1 = TOP_Y * FRAC;
    if(py1 > (BOT_Y - PADDLE_HEIGHT) * FRAC)
        py1 = (BOT_Y - PADDLE_HEIGHT) * FRAC;
 
    if(serving)
    {
        bx = PADDLE_WIDTH * FRAC;
        by = py0 + (PADDLE_HEIGHT - BALL_SIZE) * FRAC / 2;
        if($just_pressed(A_BUTTON))
        {
            serving = false;
            vx = BALL_SPEED;
            vy = -BALL_SPEED;
       }
    }
    else if(ai_serving)
    {
        bx = (128 - PADDLE_WIDTH - BALL_SIZE) * FRAC;
        by = py1 + (PADDLE_HEIGHT - BALL_SIZE) * FRAC / 2;
        ai_serving_wait = ai_serving_wait - 1;
        if(ai_serving_wait <= 0)
        {
           ai_serving = false;
           vx = -BALL_SPEED;
           vy = -BALL_SPEED;
        }
    }
    else
    {
        bx = bx + vx;
        by = by + vy;
        if(by < TOP_Y * FRAC)
        {
            by = TOP_Y * FRAC;
            bounce_ball_off_wall();
        }
        if(by > (BOT_Y - BALL_SIZE) * FRAC)
        {
            by = (BOT_Y - BALL_SIZE) * FRAC;
            bounce_ball_off_wall();
        }
        if(bx < PADDLE_WIDTH * FRAC)
        {
            bx = PADDLE_WIDTH * FRAC;
            if(by >= py0 && by <= py0 + (PADDLE_HEIGHT - BALL_SIZE) * FRAC)
                bounce_ball_off_paddle();
            else
            {
                serving = true;
                s1 = s1 + 1;
                $tones_play(SFX_LOSS);
            }
        }
        if(bx > (128 - PADDLE_WIDTH - BALL_SIZE) * FRAC)
        {
            bx = (128 - PADDLE_WIDTH - BALL_SIZE) * FRAC;
            if(by >= py1 && by <= py1 + (PADDLE_HEIGHT - BALL_SIZE) * FRAC)
                bounce_ball_off_paddle();
            else
            {
                ai_serving = true;
                ai_serving_wait = 20;
                s0 = s0 + 1;
                $tones_play(SFX_WIN);
            }
        }
    }
}

void render()
{
    // scores
    $draw_filled_rect(0, TOP_Y - 1, 128, 1, WHITE);
    $draw_textf(0, 0, f, "%d", s0);
    {
        // right-align the AI's score
        char[8] buf;
        $format(buf, "%d", s1);
        $draw_text(129 - $text_width(f, buf), 0, f, buf);
    }
    
    $draw_text_P(52, 0, f, "PONG!");

    // paddles
    $draw_filled_rect(0, py0 / FRAC, PADDLE_WIDTH, PADDLE_HEIGHT, WHITE);
    $draw_filled_rect(128 - PADDLE_WIDTH, py1 / FRAC, PADDLE_WIDTH, PADDLE_HEIGHT, WHITE);
    
    // ball
    $draw_sprite(bx / FRAC, by / FRAC, img_ball, 0);
        
    // bottom line
    $draw_filled_rect(0, BOT_Y, 128, 1, WHITE);
}

void new_game()
{
    constexpr int midy = (BOT_Y + TOP_Y - PADDLE_HEIGHT) / 2;
    py0 = FRAC * midy;
    py1 = FRAC * midy;

    serving = true;
    ai_serving = false;
}
